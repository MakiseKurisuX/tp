plugins {
    id 'java'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '9.0.0-beta9'
    id 'application'
    id 'jacoco'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

checkstyle {
    toolVersion = '10.12.4'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

application {
    mainClass.set("seedu.address.Main")
}

task coverage(type: JacocoReport) {
    dependsOn test

    sourceDirectories.from files(sourceSets.main.allSource.srcDirs)
    classDirectories.from files(sourceSets.main.output)
    executionData.from files(jacocoTestReport.executionData)
    afterEvaluate {
        classDirectories.from files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/*.jar'])
        })
    }
    reports {
        html.required = true
        xml.required = true
    }
}

dependencies {
    String jUnitVersion = '5.11.4'

    def platforms = ["win", "linux", "mac"]
    def javafxDependency = ["javafx-controls", "javafx-fxml", "javafx-graphics"]

    for (plt in platforms) {
        for (dep in javafxDependency) {
            runtimeOnly "org.openjfx:$dep:$javafx.version:$plt"
        }
    }

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.0'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.7.4'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnitVersion
}

javafx {
    version = '21.0.6'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

run {
    enableAssertions = true
}

shadowJar {
    archiveFileName = 'InsureBook.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

defaultTasks 'clean', 'test'
